plugins {
    alias libs.plugins.quilt.loom
    id 'idea'
    id 'com.matthewprenger.cursegradle' version '1.+'
    id "com.modrinth.minotaur" version "2.+"
}

archivesBaseName = "${mod_id}-${libs.versions.minecraft.get()}-quilt"

loom {
    runs {
        client {
            client()
            setConfigName("Quilt Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Quilt Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }

    mixin {
        useLegacyMixinAp = true
        defaultRefmapName = 'cobbledpaths.refmap.json'
        //add 'main', 'cobbledpaths.refmap.json'
    }
}

dependencies {
    minecraft libs.minecraft
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${libs.versions.parchment.minecraft.get()}:${libs.versions.parchment.mappings.get()}@zip")
    }

    modImplementation libs.quilt.loader

    modImplementation libs.qfapi

    modImplementation(libs.cgl.quilt) {
        exclude group: 'org.quiltmc'
    }

    runtimeOnly(libs.groovyduvet.groovy) {
        exclude group: 'org.quiltmc'
    }
    modImplementation(libs.groovyduvet.all) {
        exclude group: 'org.quiltmc'
    }

    compileOnly(project(path: ":Common", configuration: "namedElements")) {
        exclude group: 'org.quiltmc'
    }
}

tasks.withType(GroovyCompile) {
    source project(":Common").sourceSets.main.allGroovy
}

tasks.withType(JavaCompile) {
    source project(":Common").sourceSets.main.allJava
}

processResources {
    from project(':Common').sourceSets.main.resources
    exclude 'mods.groovy'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = "${mod_id}-${libs.versions.minecraft.get()}-quilt"
            version = project.version
            from components.java
        }
    }
}

if (project.hasProperty("curseforge_key")) {
    curseforge {
        apiKey = project.curseforge_key
        project {
            id = '573329'
            releaseType = "${project.release_type}"
            addGameVersion "${libs.versions.minecraft.get()}"
            addGameVersion 'Quilt'
            mainArtifact(remapJar) {
                displayName = "Cobbled Paths [Quilt] v$project.version"
            }
            relations {
                requiredDependency 'groovyduvet'
                requiredDependency 'qsl'
            }
        }
        options {
            forgeGradleIntegration = true
        }
    }
}

if (project.hasProperty("modrinth_key")) {
    modrinth {
        token = project.modrinth_key
        projectId = 'Si1Oa29R'
        versionNumber = "$project.version"
        versionType = "${project.release_type}"
        detectLoaders = false
        uploadFile = remapJar
        gameVersions = ["${libs.versions.minecraft.get()}"]
        loaders = ['quilt']
        dependencies {
            required.project 'groovyduvet'
            required.project 'qsl'
        }
    }
}
